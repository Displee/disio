buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
    id 'maven'
    id 'signing'
}

repositories {
    mavenCentral()
}

group 'com.displee'
archivesBaseName = "disio"
version '1.1'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testImplementation(
            'org.assertj:assertj-core:3.12.2',
            'org.junit.jupiter:junit-jupiter-api:5.4.2'
    )
    testRuntime('org.junit.jupiter:junit-jupiter-engine:5.4.2')
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    useJUnitPlatform()
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'DisIO'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'A small standalone library that allows you to perform I/O operations.'
                url 'https://github.com/Displee/disio'

                scm {
                    connection 'scm:git:git://github.com/Displee/disio.git'
                    developerConnection 'scm:git:ssh://git@github.com/Displee/disio.git'
                    url 'https://github.com/Displee/disio'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://github.com/Displee/disio/blob/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id 'Displee'
                        name 'Yassin Amhagi'
                        email 'displee@hotmail.com'
                    }
                }
            }
        }
    }
}